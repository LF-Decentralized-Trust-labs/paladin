/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

ext {
    goFiles = fileTree(".") {
        include "internal/**/*.go"
    }
}

configurations {
    // Resolvable configurations (inputs)
    contractCompile { canBeConsumed = false }
    protocCompile { canBeConsumed = false }
}

dependencies {
    contractCompile project(path: ":solidity", configuration: "compiledContracts")
    protocCompile project(path: ":kata", configuration: "protocCompile")
}

task copySolidity(type: Copy) {
    inputs.files(configurations.contractCompile)
    from fileTree(configurations.contractCompile.asPath) {
        include 'contracts/zeto/ZetoSample.sol/ZetoSample.json'
        include 'contracts/zeto/ZetoSampleFactory.sol/ZetoSampleFactory.json'
    }
    into 'internal/zeto/abis'

    // Flatten all paths into the destination folder
    eachFile { path = name }
    includeEmptyDirs = false
}

task startTestbed(type: DockerCompose) {
    dependsOn ':testinfra:besuBootstrapTool' // TODO fix?
    composeFile '../../testinfra/docker-compose-test.yml'
    composeFile 'docker-compose-test.yml'
    projectName 'paladin-zeto'
    args 'up', '-d', '--build'
}

task stopTestbed(type: DockerCompose) {
    composeFile '../../testinfra/docker-compose-test.yml'
    composeFile 'docker-compose-test.yml'
    projectName 'paladin-zeto'
    args 'down'
}

task test(type: Exec) {
    inputs.files(configurations.protocCompile)
    inputs.files(goFiles)
    outputs.dir('coverage')

    workingDir '.'
    executable 'go'
    args 'test'
    args './internal/...'
    args '-cover'
    args '-covermode=atomic'
    args '-timeout=30s'
    args "-test.gocoverdir=${projectDir}/coverage"

    dependsOn copySolidity
    dependsOn startTestbed

    ignoreExitValue true

    doLast {
        String composeLogs = getProject().findProperty('composeLogs')
        ExecResult execResult = executionResult.get()
        if (execResult.exitValue != 0 && composeLogs == "true") {
            startTestbed.dumpLogs("testbed")
        }
        execResult.assertNormalExitValue()
    }
}

task build {
    dependsOn test
}

task clean {
    dependsOn stopTestbed
}
