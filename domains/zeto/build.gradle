/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

import org.apache.tools.ant.taskdefs.condition.Os

ext {
    goFiles = fileTree(".") {
        include "internal/**/*.go"
        include "pkg/**/*.go"
    }
    zetoVersion = "v0.0.2"
    zkpOut = "${projectDir}/zkp"
    toolsOut = "${projectDir}/tools"
}

configurations {
    // Resolvable configurations
    contractCompile {
        canBeConsumed = false
        canBeResolved = true
    }
    toolkitProtoCompiled {
        canBeConsumed = false
        canBeResolved = true
    }
    coreProtoCompiled {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    contractCompile project(path: ":solidity", configuration: "compiledContracts")
    toolkitProtoCompiled project(path: ":toolkit:go", configuration: "protoCompiled")
    coreProtoCompiled project(path: ":core:go", configuration: "protoCompiled")
}

task downloadZetoProver {
    def outname = "zeto-wasm-${zetoVersion}.tar.gz"
    def url = "https://github.com/hyperledger-labs/zeto/releases/download/${zetoVersion}/${outname}"
    def f = new File(toolsOut, outname)
    doFirst {
        mkdir(f.parent)
        new URL(url).withInputStream{ i -> f.withOutputStream{ it << i }}
        f.setExecutable(true)
    }
    outputs.file(f)
}

task downloadZetoTestProvingKeys {
    def outname = "zeto-test-proving-keys-${zetoVersion}.tar.gz"
    def url = "https://github.com/hyperledger-labs/zeto/releases/download/${zetoVersion}/${outname}"
    def f = new File(toolsOut, outname)
    doFirst {
        mkdir(f.parent)
        new URL(url).withInputStream{ i -> f.withOutputStream{ it << i }}
        f.setExecutable(true)
    }
    outputs.file(f)
}

task downloadZetoCompiledContracts {
    def outname = "zeto-contracts-${zetoVersion}.tar.gz"
    def url = "https://github.com/hyperledger-labs/zeto/releases/download/${zetoVersion}/${outname}"
    def f = new File(toolsOut, outname)
    doFirst {
        mkdir(f.parent)
        new URL(url).withInputStream{ i -> f.withOutputStream{ it << i }}
        f.setExecutable(true)
    }
    outputs.file(f)
}

task extractZetoProverArtifacts(type: Copy, dependsOn: [downloadZetoProver, downloadZetoTestProvingKeys]) {
    from tarTree("${toolsOut}/zeto-wasm-${zetoVersion}.tar.gz")
    from tarTree("${toolsOut}/zeto-test-proving-keys-${zetoVersion}.tar.gz")
    into zkpOut

    outputs.dir(zkpOut)
}

task extractZetoContracts(type: Copy, dependsOn: [downloadZetoCompiledContracts]) {
    from tarTree("${toolsOut}/zeto-contracts-${zetoVersion}.tar.gz")
    into "${zkpOut}/contracts"

    outputs.dir(zkpOut)
}

task copySolidity(type: Copy, dependsOn: [extractZetoContracts, ":solidity:compile"]) {
    inputs.files(fileTree("${zkpOut}/contracts"))
    from fileTree("${zkpOut}/contracts") {
        include '**/*.json'
        exclude '**/*.dbg.json'
    }
    from fileTree("${rootDir}/solidity/artifacts") {
        include 'contracts/zeto/ZetoFactory.sol/ZetoFactory.json'
    }
    into 'internal/zeto/abis'

    // Flatten all paths into the destination folder
    eachFile { path = name }
    includeEmptyDirs = false
}

task test(type: Exec) {
    environment "CIRCUITS_ROOT", zkpOut
    environment "PROVING_KEYS_ROOT", zkpOut

    inputs.files(configurations.toolkitProtoCompiled)
    inputs.files(configurations.coreProtoCompiled)
    inputs.files(goFiles)
    inputs.file('./internal/zeto/zeto.config.yaml')
    outputs.dir('coverage')

    workingDir '.'
    executable 'go'
    args 'test'
    args '-v'
    args './internal/...'
    args './pkg/...'
    args '-cover'
    args '-covermode=atomic'
    args '-timeout=30s'
    args "-test.gocoverdir=${projectDir}/coverage"

    dependsOn copySolidity
    dependsOn extractZetoProverArtifacts
    dependsOn ':testinfra:startTestInfra'
    helpers.dumpLogsOnFailure(it, ':testinfra:startTestInfra')
}

task build {
    dependsOn test
}

task clean(type: Delete) {
    delete 'coverage'
    delete 'internal/zeto/abis'
    delete zkpOut
    delete toolsOut
}
