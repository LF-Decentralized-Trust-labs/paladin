plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

mainClassName = 'io.kaleido.Main'
group = 'io.kaleido'
version = '1.0-SNAPSHOT'

task wrapper(type: Wrapper){}

application {
    applicationDefaultJvmArgs = ["-Djava.library.path=" + file("${buildDir}/libs/paladin/shared").absolutePath]
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation project(path: ':kata', configuration: 'libkata')
}

test {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    dependsOn ':kata:buildSharedLibrary'
}

tasks.named('generateProto') {
    dependsOn ':kata:protoc'
}

tasks.named('processResources') {
    dependsOn ':kata:protoc'
}

def grpcVersion = '1.65.0'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation("io.netty:netty-transport-native-kqueue:4.1.111.Final") {
        artifact {
            classifier = "osx-aarch_64"
        }
    }

    implementation 'net.java.dev.jna:jna-platform:5.14.0'

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
        proto {
            srcDirs '../kata/pkg/proto'
        }
    }
}

