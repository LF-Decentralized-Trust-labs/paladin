import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

mainClassName = 'io.kaleido.Main'
group = 'io.kaleido'
version = '1.0-SNAPSHOT'

task wrapper(type: Wrapper){}

application {
    applicationDefaultJvmArgs = ["-Djna.library.path=" + file("${rootDir}/kata").absolutePath]
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    systemProperty "jna.library.path", file("${rootDir}/kata").absolutePath
}

configurations {
    toolkitProtoDefinitions {
        canBeConsumed = false
    }
    libkata {
        canBeConsumed = false
    }
}

dependencies {
    toolkitProtoDefinitions project(path: ":toolkit_proto", configuration: "toolkitProtoDefinitions")
    testImplementation project(path: ":kata", configuration: "libkata")
}

tasks.register('collectProtocFiles', Copy) {
    from configurations.toolkitProtoDefinitions.asFileTree
    into "src/main/proto"
}

tasks.register('cleanProto', Delete) {
    delete 'src/main/proto'
}

tasks.named('generateProto') {
    dependsOn 'collectProtocFiles'
}

tasks.named('processResources') {
    dependsOn 'generateProto'
}

tasks.named('processResources') {
    dependsOn ':kata:protoc'
}

tasks.named('clean') {
    dependsOn tasks.cleanProto
}

def grpcVersion = '1.65.0'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    implementation 'io.netty:netty-all:4.1.111.Final' // supports Java 16 UDS natively since Netty 4.4.110
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2'

    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'

    implementation project(path: ':kata', configuration: 'libkata')
    implementation 'net.java.dev.jna:jna-platform:5.14.0'
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

