/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.LineContains

ext {
    goFiles = files(
        getProject().fileTree("internal").matching { include "**/*.go" },
        getProject().fileTree("pkg").matching { include "**/*.go" },
        getProject().fileTree("componenttest").matching { include "**/*.go" },
    )

    targetCoverage = 91
    maxCoverageBarGap = 1
    coverageExcludedPackages = [
        'github.com/kaleido-io/paladin/kata/pkg/proto'
    ]
}

task installTools(type:Exec) {
    workingDir '.'
    environment('GOBIN', "${projectDir}/bin")
    executable 'go'
    args 'install'
    inputs.files('go.mod')
    outputs.files([
        "${projectDir}/bin/protoc-gen-go",
        "${projectDir}/bin/protoc-gen-go-grpc",
        "${projectDir}/bin/golangci-lint",
    ])
    // We use go.mod for dependency version management
    // tools/tools.go tracks deps too - to avoid go mod tidy discarding them all
    args 'google.golang.org/protobuf/cmd/protoc-gen-go'
    args 'google.golang.org/grpc/cmd/protoc-gen-go-grpc'
    args 'github.com/golangci/golangci-lint/cmd/golangci-lint'
}

task protoc(type:Exec, dependsOn: tasks.installTools) {
    workingDir '.'
    inputs.files(fileTree("${projectDir}").matching {
        include "**/*.proto"
    })
    outputs.files(fileTree("${projectDir}").matching {
        include "**/*.pb.go"
    })
    environment 'PATH', "${projectDir}/bin" + File.pathSeparator + System.getenv('PATH')
    executable 'protoc'
    args "--go_out=${projectDir}"
    args "--go-grpc_out=${projectDir}"
    args "--proto_path=${projectDir}"
    args inputs.files
}

task clean(type:Exec) {
    workingDir '.'
    // This will delete anything ignored with a .gitignore
    executable 'git'
    args "clean"
    args "-fdX"
}

task goGet(type:Exec, dependsOn:tasks.protoc) {
    workingDir '.'

    inputs.files('go.mod')
    inputs.files(goFiles)
    outputs.files('go.sum')

    executable 'go'
    args 'get'
}

task lint(type: Exec, dependsOn:tasks.protoc) {
    workingDir '.'

    inputs.files(goFiles);
    outputs.files([]);
    environment 'GOGC', '20'

    executable "${projectDir}/bin/golangci-lint"
    args 'run'
    args '-v'
    args '--color=always'
    args '--timeout', '5m'
}

// Must NOT depend on clean (which would delete all deliberately ignored files like .vscode config)
task buildSharedLibrary(type:Exec, dependsOn:[tasks.goGet,tasks.lint]) {
    workingDir '.'

    def libName;
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        libName = "libkata.dll"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        libName = "libkata.dylib"
    } else {
        libName = "libkata.so"
    }

    inputs.files(goFiles)
    outputs.files([libName, 'libkata.h'])

    environment("CGO_ENABLED", "1")

    executable 'go'
    args 'build'
    args '-o', libName
    args '-buildmode=c-shared'
    args 'kata.go'
}

class DockerCompose extends Exec {
    DockerCompose() {
        def dockerComposeV2Check = getProject().exec({
            ignoreExitValue = true
            executable "docker"
            args "compose", "version"
            standardOutput = new ByteArrayOutputStream()
            errorOutput = new ByteArrayOutputStream()
        })
        if (dockerComposeV2Check.getExitValue() == 0){
            executable 'docker'
            args 'compose'
        } else {
            executable 'docker-compose'
        }
        args '-f'
    }
}

class DockerComposeTest extends DockerCompose {
    DockerComposeTest() {
        def dbComposeFile = "${getProject().projectDir}/docker-compose-test.yml"
        args dbComposeFile
    }
}

class DockerComposeDemo extends DockerCompose {
    DockerComposeDemo() {
        def dbComposeFile = "${getProject().rootDir}/demo/docker-compose.yml"
        args dbComposeFile
    }
}

task startDb(type: DockerComposeTest) {
    mustRunAfter(tasks.protoc)
    args 'up', '-d'
}

task startDemoDb(type: DockerComposeDemo) {
    mustRunAfter(tasks.protoc)
    args 'up', '-d'
}

task stopDb(type: DockerComposeTest) {
    args 'down'
}

task stopDemoDb(type: DockerComposeDemo) {
    args 'down'
}

abstract class UnitTests extends Exec {
    {
        inputs.files(getProject().goFiles)
        outputs.dir('coverage')

        workingDir '.'
        executable 'go'
        args 'test'
        args './pkg/...', './internal/...'
        args '-cover'
        args '-covermode=atomic'
        args '-timeout=30s'
        args "-test.gocoverdir=${getProject().projectDir}/coverage"
    }
}

abstract class ComponentTest extends Exec {
    {
        inputs.files(getProject().goFiles)
        outputs.dir('coverage')

        workingDir '.'
        executable 'go'
        args 'test'
        args './componenttest'
        args '-cover'
        args "-coverpkg=github.com/kaleido-io/paladin/..."
        args '-covermode=atomic'
        args '-timeout=30s'
        args "-test.gocoverdir=${getProject().projectDir}/coverage"
    }
}

task setupCoverage(dependsOn: tasks.protoc) {
    inputs.files(getProject().goFiles)
    outputs.dir('coverage')
    doLast {
        delete fileTree (project.mkdir('coverage')) {
            include "coverage.txt"
            include "covcounters.*"
            include "covmeta.*"
        }
    }
}

task unitTestSQLite(type: UnitTests, dependsOn: [tasks.setupCoverage, tasks.goGet]) {
}

task unitTestPostgres(type: UnitTests, dependsOn: [tasks.setupCoverage, tasks.goGet, tasks.startDb]) {
    args '-tags', "testdbpostgres"
}

task componentTestSQLite(type: ComponentTest, dependsOn: [tasks.setupCoverage, tasks.goGet]) {
}

// task componentTestSQLite(type: ComponentTest, dependsOn: [tasks.setupCoverage, tasks.goGet, tasks.startDb]) {
// }

task buildCoverageTxt(type: Exec, dependsOn: [tasks.protoc,
        tasks.unitTestSQLite,
        tasks.unitTestPostgres,
        tasks.componentTestSQLite,
        // tasks.componentTestPostgres
    ]) {
    inputs.files(fileTree(project.mkdir('coverage')) {
        include "covcounters.*"
        include "covmeta.*"
    })
    outputs.files('coverage/coverage_unfiltered.txt')
    executable 'go'
    args 'tool', 'covdata', 'textfmt'
    args '-i', "${projectDir}/coverage"
    args '-o', "${projectDir}/coverage/coverage_unfiltered.txt"

}

task aggregateCoverage(type: Copy, dependsOn: tasks.buildCoverageTxt) {
    from 'coverage/coverage_unfiltered.txt'
    into 'coverage'
    outputs.files('coverage/coverage.txt')
    eachFile { fcd ->
        fcd.setRelativePath(fcd.relativePath.replaceLastName('coverage.txt'))
    }
    filter(LineContains, negate: true, contains: getProject().ext.coverageExcludedPackages)
}

task checkCoverage(dependsOn: [tasks.aggregateCoverage]) {
    inputs.files('coverage/coverage.txt')
    outputs.files([])

    doLast {
        def coverageOutput = ("go tool cover -func=${projectDir}/coverage/coverage.txt").execute().text
        def totalCoverage = coverageOutput.readLines().find { it.contains('total:') }?.split()?.last()?.replace('%', '')
        println "Coverage is ${totalCoverage}%"
        if (totalCoverage && totalCoverage.toFloat() < targetCoverage) {
            throw new GradleException("ERROR: Coverage is below ${targetCoverage}% (current coverage: ${totalCoverage}%)")
        } else if ( totalCoverage.toFloat() - targetCoverage > maxCoverageBarGap) {
            throw new GradleException("ERROR: The target coverage ${targetCoverage}% is below the current coverage: ${totalCoverage}% for more than ${maxCoverageBarGap}%, please update the targetCoverage value in ./kata/build.gradle file")
        } else {
            println "Coverage is above ${targetCoverage}%, current coverage: ${totalCoverage}%"
        }
    }
}

task test() {
    finalizedBy [
        tasks.checkCoverage,
    ]
}

task testcov(type: Exec, dependsOn: [tasks.test, tasks.aggregateCoverage]) {
    inputs.files('coverage/coverage.txt')
    workingDir '.'
    executable 'go'
    args 'tool'
    args 'cover'
    args '-html=coverage/coverage.txt'
}

task build {
    dependsOn tasks.lint
    dependsOn tasks.test
    dependsOn tasks.aggregateCoverage
    dependsOn tasks.checkCoverage
}

configurations {
    libkata
    protocFiles {
        canBeConsumed = true
        canBeResolved = false
    }
}

tasks.register('zipProtocFiles', Zip) {  
    from fileTree("${projectDir}/pkg").matching {
        include "**/*.proto"
    }
    eachFile { fcd ->
        fcd.setRelativePath(fcd.relativePath.prepend("pkg"))
    }
    archiveFileName = "proto.zip"
    destinationDirectory = file("$projectDir")
    includeEmptyDirs = false
    dependsOn('protoc')
}

artifacts {
    protocFiles(zipProtocFiles)
}

dependencies {
    libkata files(buildSharedLibrary)
}


