import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

mainClassName = 'io.kaleido.Main'
group = 'io.kaleido'
version = '1.0-SNAPSHOT'

task wrapper(type: Wrapper){}

application {
    applicationDefaultJvmArgs = ["-Djna.library.path=" +
        file("${rootDir}/core/go").absolutePath + ":" + file("${rootDir}/toolkit/go").absolutePath ]
}

repositories {
    mavenCentral()
    maven {
        url 'https://hyperledger.jfrog.io/hyperledger/besu-maven'
        content { includeGroupByRegex('org\\.hyperledger\\..*') }
    }
}

test {
    dependsOn ':testinfra:startTestInfra'
    testLogging {
        showStandardStreams = true
    }
    useJUnitPlatform()
    systemProperty "jna.library.path", file("${rootDir}/core/go").absolutePath + ":" + file("${rootDir}/toolkit/go").absolutePath
}

configurations {
    // Resolvable configurations
    toolkitProtoDefinitions {
        canBeConsumed = false
        canBeResolved = true
    }
    libcore {
        canBeConsumed = false
        canBeResolved = true
    }
    libstarter {
        canBeConsumed = false
        canBeResolved = true
    }
}

tasks.register('collectProtocFiles', Copy) {
    from configurations.toolkitProtoDefinitions.asFileTree
    into "src/main/proto"
}

tasks.register('cleanProto', Delete) {
    delete 'src/main/proto'
}

tasks.named('generateProto') {
    dependsOn 'collectProtocFiles'
}

tasks.named('processResources') {
    dependsOn 'generateProto'
}

tasks.named('processResources') {
    dependsOn ':core:go:protoc'
}

tasks.named('clean') {
    dependsOn tasks.cleanProto
}

def grpcVersion = '1.65+'
def protobufVersion = '3.25+'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    implementation 'io.netty:netty-all:4.1+'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17+'

    implementation 'org.apache.logging.log4j:log4j-core:2.23+'
    implementation 'org.hyperledger.besu:besu-datatypes:24.5.4'

    toolkitProtoDefinitions project(path: ":toolkit:proto", configuration: "protoDefinitions")

    testImplementation project(path: ":core:go", configuration: "libcore")
    testImplementation project(path: ":toolkit:go", configuration: "libstarter")
    testImplementation project(path: ":solidity", configuration: "compiledContracts")

    implementation 'net.java.dev.jna:jna-platform:5.14+'
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    compileOnly "org.apache.tomcat:annotations-api:6.0+"

    testImplementation platform('org.junit:junit-bom:5.9+')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation project(path: ':toolkit:go', configuration: 'libstarter')

}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

