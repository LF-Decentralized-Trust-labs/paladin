/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

ext {
    goFiles = fileTree(".") {
        include "internal/**/*.go"
        include "pkg/**/*.go"
        include "main.go"
    }
}

configurations {
    // Resolvable configurations
    toolkitGo {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    toolkitGo project(path: ":toolkit:go", configuration: "goSource")
}

task lint(type: Exec, dependsOn: ":installGolangCILint") {
    workingDir '.'

    helpers.lockResource(it, "lint.lock")
    inputs.files(configurations.toolkitGo)
    inputs.files(goFiles);
    environment 'GOGC', '20'

    executable "golangci-lint"
    args 'run'
    args '-v'
    args '--color=always'
    args '--timeout', '5m'
}

task goUnitTest(type: GoTestRunner, dependsOn: ':testinfra:startTestInfra') {

    addTestArgs("./internal/...")
    def relativePath = project.path.replace(":", "_").replaceAll("^_","").replace("/", "_")
    def prefix = "${relativePath}.unit_test"
    resultsOutputFile = project.helpers.generateResultsFileName(name, prefix)
}

task test(dependsOn: goUnitTest) {
    mustRunAfter goUnitTest
    doLast {
        project.helpers.dumpLogsOnFailure(goUnitTest, ':testinfra:startTestInfra')
    }
}

task build {
    dependsOn lint
    dependsOn test
}

task clean(type: Delete) {
    delete 'coverage'
    delete 'results'
}
