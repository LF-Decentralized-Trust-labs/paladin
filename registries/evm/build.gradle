/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

ext {
    goFiles = fileTree(".") {
        include "internal/**/*.go"
        include "pkg/**/*.go"
        include "main.go"
    }
}

configurations {
    // Resolvable configurations
    toolkitGo {
        canBeConsumed = false
        canBeResolved = true
    }
    compiledContracts {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    toolkitGo project(path: ":toolkit:go", configuration: "goSource")
    compiledContracts project(path: ":solidity", configuration: "compiledContracts")
}

task copyContracts(type: Copy) {
    inputs.files(configurations.compiledContracts)
    from fileTree(configurations.compiledContracts.asPath) {
        include 'contracts/registry/IdentityRegistry.sol/IdentityRegistry.json'
    }
    into 'internal/evmregistry/abis'

    // Flatten all paths into the destination folder
    eachFile { path = name }
    includeEmptyDirs = false
}

task lint(type: Exec, dependsOn: [":installGolangCILint",copyContracts]) {
    workingDir '.'

    helpers.lockResource(it, "lint.lock")
    inputs.files(configurations.toolkitGo)
    inputs.files(goFiles);
    environment 'GOGC', '20'

    executable "golangci-lint"
    args 'run'
    args '-v'
    args '--color=always'
    args '--timeout', '5m'
}

task test(type: Exec, dependsOn: [copyContracts]) {
    inputs.files(configurations.toolkitGo)
    inputs.files(goFiles)
    outputs.dir('coverage')

    workingDir '.'
    executable 'go'
    args 'test'
    args './internal/...'
    args '-cover'
    args '-covermode=atomic'
    args '-timeout=30s'
    if (project.findProperty('verboseTests') == 'true') {
        args '-v'
    }    
    args "-test.gocoverdir=${projectDir}/coverage"

    dependsOn ':testinfra:startTestInfra'
}

task build {
    dependsOn lint
    dependsOn test
}

task clean(type: Delete) {
    delete 'coverage'
}
