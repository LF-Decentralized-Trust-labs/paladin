import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'application'
    id 'c'
}

group = 'io.kaleido'
version = '1.0-SNAPSHOT'

application {
    applicationDefaultJvmArgs = ["-Djava.library.path=" + file("${buildDir}/libs/paladin/shared").absolutePath]
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
        arm64 {
            architecture "arm64"
        }
        auto {
            if (operatingSystem.macOsX) {
                architecture "arm64"
            } else {
                architecture "x86"
            }
        }
    }

    components {
        paladin(NativeLibrarySpec) {
            targetPlatform "auto"
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/darwin"
                    cCompiler.args '-I', "${projectDir}/../gable"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                    linker.args '-shared', "${projectDir}/../gable/gable.so"
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${projectDir}/../gable"
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                    linker.args '-shared', "${projectDir}/../gable/gable.so"
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args '-I', "${projectDir}/../gable"
                    cCompiler.args "-I${jvmHome}/include"
                    cCompiler.args "-I${jvmHome}/include/win32"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${projectDir}/../gable"
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/freebsd"
                }
            }
        }
    }
}

classes.dependsOn 'paladinSharedLibrary'
